/*plugins {
    id 'com.android.application'
    id 'kotlin-android'
}*/

/**
 * rootProject.projectDir 是工程的根目录
 * apply from 引入构建脚本的操作, 等同于将该构建脚本原封不动拷贝到此处
 */
apply from: "${rootProject.rootDir}/common.gradle"

if (project.isModuleDebug) {
    /**
     * 组件化 : 在 Debug 开发阶段, 每个模块都可以独立运行, 因此相关的模块都是 Application 应用模块,
     *         此时, isModuleDebug 设置为 true. ( 单独运行 )
     * 组件化导入 com.android.application 插件
     */
    apply plugin: 'com.android.application'

} else {
    /**
     * 模块化 : 在 Release 发布阶段, 只有一个壳应用是可以独立运行的, 其它所有的模块都作为依赖库存在,
     *         此时, isModuleDebug 设置为 false. ( 非单独运行 )
     * 模块化导入 com.android.library 插件
     */
    apply plugin: 'com.android.library'
}

/**
 * 导入 Kotlin 插件
 */
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 32
    buildToolsVersion "32.0.0"

    defaultConfig {
        if (project.isModuleDebug) {
            /**
             * 模块化时才能设置 applicationId
             * 依赖库设置 applicationId 编译时会报错
             */
            applicationId "kim.hsl.business"
        }
        minSdkVersion 18
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            if (project.isModuleDebug) {
                //可运行模块
                manifest.srcFile "src/main/debug/AndroidManifest.xml"
            } else {
                //依赖库模块
                manifest.srcFile "src/main/AndroidManifest.xml"
            }
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}